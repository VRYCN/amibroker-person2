//SetOptions
SetOption("InitialEquity", 100000);
SetOption("MaxOpenPositions", 20);
SetOption("MinShares", 100);
RoundLotSize = 100;

SetOption("CommissionMode", 1); //1 = Percent Of Trade
SetOption("CommissionAmount", 0.1578);
SetTradeDelays(1, 1, 0, 0);

//Signals
BuyPrice = Close;
PriceInRange = Close <= 50;
PositiveClose = (Close - Open) > 0;
PositiveDirection = PDI(14) > MDI(14);
PositiveDirectionAggressive = PDI(100) > MDI(100);
EnoughVolume = Volume > 100000;
SET = Foreign("SET","Close",True);
Bear = SET < MA(SET, 150);
BuyNormal = Close >= HHV(Close, 50) && PriceInRange && PositiveClose && PositiveDirection && EnoughVolume;
BuyBear = Close >= HHV(Close, 400) && PriceInRange && PositiveClose && PositiveDirectionAggressive && EnoughVolume;
Buy = IIf(Bear, BuyBear, BuyNormal);

SellPrice = Open;
Sell = 0;

//Add column for display
Filter= Buy OR Sell;
AddColumn(Close, "Close");
AddColumn(Volume, "Volume");
AddColumn( IIf( Buy, 66, 83 ), "Signal", formatChar );

//Position Sizing
SetPositionSize(5, spsPercentOfEquity);

//Position Score
PositionScore = Volume;

//Apply Stop
StopLength = 3.5 * ATR(21);
ApplyStop(stopTypeTrailing, stopModePoint, StopLength, 2, True, 1);
