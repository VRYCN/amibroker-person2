//SetOptions
SetOption("InitialEquity", 100000);
SetOption("MaxOpenPositions", 20);
SetOption("MinShares", 100);
RoundLotSize = 100;

SetOption("CommissionMode", 1); //1 = Percent Of Trade
SetOption("CommissionAmount", 0.2);
SetTradeDelays(1, 1, 0, 0);

//Optimization Parameter
//shortDI = Optimize("ShortDI", 7, 1, 14, 1);
shortDI = 7;
//longDI = Optimize("LongDI", 20, 10, 20, 1);
longDI = 10;

//highMomentum = Optimize("HighMomentum", 80, 10, 90, 5);
highMomentum = 80;
//lowMomentum = Optimize("LowMomentum", 50, 10, 80, 5);
lowMomentum = 50;

stopPercent = Optimize("StopPercent", 25, 10, 30, 2);

//Signals
Stoch = StochK(14,3);
BuyPrice = Close;
SellPrice = Open;
PriceInRange = Close < 50;
PositiveClose = Close > Open;
EnoughVolume = Volume > 1000000;
EnoughAvgVolume = MA(Volume, 20) > 500000;
HaveMomentum = Stoch > highMomentum;
LostMomentum = Stoch < lowMomentum;
MomentumIncreasing = Stoch > Ref(Stoch, -1);
PlusDIS = PDI(shortDI);
MinusDIS = MDI(shortDI);
PlusDIL = PDI(longDI);
MinusDIL = MDI(longDI);
UpTrend = PlusDIS > MinusDIS;
DownTrend = PlusDIL < MinusDIL;
YesterdayDownTrend = Ref(PlusDIS,-1) < Ref(MinusDIS,-1);
SET = Foreign("SET","Close",True);
Bear = SET < MA(SET, 150);
Buy = PriceInRange && EnoughVolume && EnoughAvgVolume && YesterdayDownTrend && UpTrend && HaveMomentum && MomentumIncreasing && NOT Bear;
Sell = DownTrend && LostMomentum;

//Position Sizing
SetPositionSize(5, spsPercentOfEquity);

//Position Score
PositionScore =  StochK(14, 3);

//Apply Stop
ApplyStop(stopTypeTrailing, stopModePercent, stopPercent, 2, True, 1);

//Add column for display
Filter= Buy;
AddColumn(Close, "Close");
AddColumn(Volume, "Volume");
AddColumn(IIf( Buy, 66, 83 ), "Signal", formatChar );
AddColumn(PositionScore, "Score");
AddColumn(StochK(14,3), "Momentum");
AddColumn(UpTrend, "UpTrend");
AddColumn(PlusDIS, "+DI Short");
AddColumn(MinusDIS, "-DI Short");
AddColumn(PlusDIL, "+DI Long");
AddColumn(MinusDIL, "-DI Long");
