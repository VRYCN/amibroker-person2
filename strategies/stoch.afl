//SetOptions
SetOption("InitialEquity", 100000);
SetOption("MaxOpenPositions", 20);
SetOption("MinShares", 100);
RoundLotSize = 100;

SetOption("CommissionMode", 1); //1 = Percent Of Trade
SetOption("CommissionAmount", 0.2);
SetTradeDelays(1, 1, 0, 0);

//Signals
Stoch = StochK(14,3);
BuyPrice = Close;
SellPrice = Open;
PriceInRange = Close < 50;
PositiveClose = Close > Open;
EnoughVolume = MA(Volume, 20) > 500000;
HaveMomentum = Stoch > 80;
LostMomentum = Stoch < 50;
MomentumIncreasing = Stoch > Ref(Stoch, -1);
PlusDI = PDI(14);
MinusDI = MDI(14);
UpTrend = PlusDI > MinusDI;
DownTrend = PlusDI < MinusDI;
SET = Foreign("SET","Close",True);
Bear = SET < MA(SET, 150);
Buy = PriceInRange && EnoughVolume && UpTrend && HaveMomentum && MomentumIncreasing && NOT Bear;
Sell = DownTrend && LostMomentum;

//Position Sizing
SetPositionSize(5, spsPercentOfEquity);

//Position Score
PositionScore =  StochK(14, 3);

//Apply Stop
stopPoint = LLV(Close, 27);
ApplyStop(stopTypeTrailing, stopModePoint, stopPoint, 2, True, 1);

//Add column for display
Filter= Buy;
AddColumn(Close, "Close");
AddColumn(Volume, "Volume");
AddColumn(IIf( Buy, 66, 83 ), "Signal", formatChar );
AddColumn(PositionScore, "Score");
AddColumn(StochK(14,3), "Momentum");
AddColumn(UpTrend, "UpTrend");
AddColumn(PlusDI, "+DI");
AddColumn(MinusDI, "-DI");
