//SetOptions
SetOption("InitialEquity", 100000);
SetOption("MaxOpenPositions", 20);
SetOption("MinShares", 100);
RoundLotSize = 100;

SetOption("CommissionMode", 1); //1 = Percent Of Trade
SetOption("CommissionAmount", 0.2);
SetTradeDelays(1, 1, 0, 0);

//Signals
BuyPrice = Close;
SellPrice = Open;
PriceInRange = Close < 50;
EnoughVolume = Volume > 1000000;
HaveMomentum = StochK(14,3) > 80;
LostMomentum = StochK(14,3) < 50;
UpTrend = PDI(14) > MDI(14);
DownTrend = PDI(14) < MDI(14);
SET = Foreign("SET","Close",True);
Bear = SET < MA(SET, 150);
Buy = PriceInRange && EnoughVolume && UpTrend && HaveMomentum && NOT Bear;
Sell = DownTrend || LostMomentum;

//Position Sizing
SetPositionSize(5, spsPercentOfEquity);

//Position Score
PositionScore = Volume;

//Apply Stop
stopPoint = LLV(Close, 10);
ApplyStop(stopTypeTrailing, stopModePoint, stopPoint, 2, True, 1);

//Add column for display
Filter= Buy;
AddColumn(Close, "Close");
AddColumn(Volume, "Volume");
AddColumn(IIf( Buy, 66, 83 ), "Signal", formatChar );
AddColumn(PositionScore, "Score");
